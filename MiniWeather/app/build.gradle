apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.haipq.mini.weather"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
        test.java.srcDirs = ['src/test/kotlin']
        androidTest.java.srcDirs = ['src/androidTest/kotlin']
    }
    productFlavors {
        dev {
            versionNameSuffix '.dev'
        }
        prod {
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dataBinding {
        enabled = true
    }
}

kapt {
    generateStubs = true
}

repositories {
    mavenCentral()
}

dependencies {

    // Jar Libs
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Dagger 2
    compile "com.google.dagger:dagger:$dagger2_version"
    kapt "com.google.dagger:dagger-compiler:$dagger2_version"

    // RxKotlin & RxAndroid
    compile "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    compile "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"

    // Network & API
    compile "com.squareup.retrofit2:retrofit:$retrofit2_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit2_version"
    compile "com.squareup.retrofit2:converter-moshi:$retrofit2_version"
    compile "com.squareup.okhttp3:okhttp:$okhttp3_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    // Databinding
    kapt "com.android.databinding:compiler:$android_databinding_version"
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    // Google Support Libs
    compile "com.android.support:appcompat-v7:$android_support_version"

    // Room Database
    compile "android.arch.persistence.room:runtime:$android_room_version"
    kapt "android.arch.persistence.room:compiler:$android_room_version"
    compile "android.arch.persistence.room:rxjava2:$android_room_version"

    // Android Test
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_core_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile "com.squareup.assertj:assertj-android:$assertj_version"
    androidTestCompile "com.squareup.retrofit2:retrofit-mock:$retrofit2_version"
    androidTestCompile "com.squareup.okhttp3:mockwebserver:$okhttp3_version"

    // Test
    testCompile "junit:junit:$junit_version"
    testCompile "android.arch.persistence.room:testing:$android_room_version"

    compile 'com.squareup.moshi:moshi-adapters:1.5.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version", "com.android.support:support-annotations:25.3.1"
    }
}

